{ config, lib, pkgs,  private, self, ... }:
let
  jmusicbot = "${config.custom.homelab.serviceDataDir}/jmusicbot";
  tgtg_volume = "${config.custom.homelab.serviceDataDir}/tgtg";

  cfg = config.custom.homelab;
in {
  options.custom.homelab = {
    enable = lib.mkEnableOption "homelab stuff";
    enablevps = lib.mkEnableOption "VPS homelab stuff";
    serviceDataDir = lib.mkOption {
      type = lib.types.path;
      default = "/var/lib";
      description = "Base directory for service data";
    };
    primaryDomain = lib.mkOption {
      type = lib.types.str;
      description =
        "Domain that points to this host. Used to expose web services.";
    };
  };

  config = lib.mkMerge [
    (lib.mkIf cfg.enable {
      virtualisation.oci-containers.backend = "podman";
      environment.systemPackages = [ ];
      systemd.tmpfiles.rules = [ ];

      networking.firewall.allowedTCPPorts = [
        80 # caddy
        443 # caddy
      ];

      #Caddy reverse proxy
      services.caddy = {
        enable = true;
        package = pkgs.caddy.withPlugins {
          plugins = [ "github.com/caddy-dns/duckdns@v0.5.0" "github.com/caddy-dns/desec@v1.0.1"];
          hash = "sha256-sMT11i51P/4+8bcliYTZmgnFa6Y0Cu2E1g2sLJaUagE=";
        };
      };
      age.secrets.duckdns.rekeyFile = /${private}/secrets/age/duckdns.age;

      systemd.services.caddy.serviceConfig = {
        EnvironmentFile = "${config.age.secrets.duckdns.path}";
      };
    })
    (lib.mkIf cfg.enablevps {
      age.secrets.tgtg.rekeyFile = "${private}/secrets/age/tgtg.age";
      virtualisation.oci-containers.backend = "docker";
      virtualisation.oci-containers.containers = {
        tgtg = {
          image = "derhenning/tgtg:latest-alpine";

          environment = {
            TZ = "Europe/Madrid";
            LOCALE = "es_ES";
            SLEEP_TIME = "60";
            TELEGRAM = "true";
          };
          environmentFiles = [ "${config.age.secrets.tgtg.path}" ];
          extraOptions = [ "--pull=always" ];
          volumes = [ "${tgtg_volume}:/tokens" ];
        };
      };
    })
  ];
}
